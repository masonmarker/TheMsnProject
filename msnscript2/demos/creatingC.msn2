# demonstrates MSNScript2's ease of ability to create and emulate another language
#
# C made a loop lol (C->Python->MSNScript2->C)
#
# author : Mason Marker
# date : 12/8/2022

import('lib/os.msn2')
clear()



# functions
@funcdefs=[]

# emulating C functions
~ printf(first, __string)->__printedchars
    -- print(?__string?)
    -- var('__printedchars', len(?__string?))

~ fwrite(path, text)->text
    -- file.create(?path?)
    -- file.write(?path?, ?text?)
~ fread(path)->read
    -- var('read', file.read(?path?))

# include macro (we cannot use the preceeding # of the include 
# because that indicates a comment in MSNScript2)
macro('include', '__m_file', =>(

    # we need to use the system call val() to extract a complex value of a variable
    import(-(val('__m_file')))
))

# macros created to assert no errors when evaluating
macro('int main() {', 'funcdef',)
macro('}', '__unused',)

# creates new syntax to set a variable
# new syntax specifically evaluates the first two arguments provided
# as per C syntax, the variable name cannot contain spaces
macro('define', '__def', =>(

    # prepares the variable definition
    __def.replace('"', ''),
    __def.strip(),

    # prepare to create the new variable
    @vn = '',
    @vl = '',
    @at_value = 0,

    # using val() for complex return values
    each( val ('__def'), 'char', =>(
        if (not(?at_value?), =>(
            if (?char? == ' ',  @at_value = 1),
            if (?char? != ' ', vn.add(?char?))
        ), vl.add(?char?))
    )),

    # strip value and evaluate according to the type contained in the value string 
    vl.strip(),
    if (?vl? != 'None' and ?vl? != None, =>(
        var('__ev', -(?vl?)),
        if (?__ev? != None, var('vl', ?__ev?))
    )),

    print('[+] creating variable:', ?vn?, 'with value:', ?vl?),

    # creates a new macro that will replace the variable name specified
    # with the value stored
    # note that macro can take a fourth argument in place of the third
    # to return a specific value as opposed to running a block of code
    macro(?vn?, '__unused', None , ?vl?)
))



# --------------------- we now in C lol ---------------------

# serialization library
include "lib/serial.msn2"

define WELCOME "hello from a C program!"
define MAX 10
define MIN 0
define PATH "localstorage/local.txt"
define TEXT "test write"

int main() {

    # C's notorious printf()
    printf("%s", WELCOME)

    # assertions, note the conformity to C's possible syntax given
    # a function declaration for assert(), not(), and equals()
    assert(equals(MAX, 10))
    assert(not(equals(MAX, 11)))

    assert(equals(0, MIN))
    assert(not(equals(1, MIN)))
    assert(not(equals(MAX, MIN)))
    assert(equals(MAX, MAX))

    # its humble fwrite()
    fwrite(PATH, TEXT)

    # and its subtle fread()
    assert(equals(fread(PATH), TEXT))
}

# -----------------------------------------------------------